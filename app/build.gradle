apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
  compileSdk 33

  defaultConfig {
    applicationId "com.michaldrabik.muzeipixelartextension"
    minSdkVersion 21
    targetSdkVersion 33
    versionCode 14
    versionName "1.2.1"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    def authorityValue = "com.michaldrabik.muzeipixelartextensionauthority"
    manifestPlaceholders = [
        pixelArtAuthority: authorityValue
    ]
    buildConfigField "String", "PIXELART_AUTHORITY", "\"${authorityValue}\""
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    config {
      keyAlias getLocalProperty("keystore.properties", "keyAlias")
      keyPassword getLocalProperty("keystore.properties", "keyPassword")
      storeFile file(getLocalProperty("keystore.properties", "storeFile"))
      storePassword getLocalProperty("keystore.properties", "storePassword")
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.config
    }
  }

  flavorDimensions "stage"
  productFlavors {
    development {
      dimension "stage"
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
    }
    production {
      dimension "stage"
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'android.arch.work:work-runtime:1.0.1'

  implementation 'com.squareup.retrofit2:retrofit:2.9.0'
  implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

  implementation 'com.google.android.apps.muzei:muzei-api:3.4.1'
  implementation 'com.google.firebase:firebase-crashlytics:18.3.6'
  implementation 'com.google.firebase:firebase-analytics:21.2.2'

  testImplementation 'junit:junit:4.13.2'
}

def getLocalProperty(String fileName, String propertyName) {
  def propsFile = rootProject.file(fileName)
  if (propsFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propsFile))
    return props[propertyName]
  } else {
    return ""
  }
}